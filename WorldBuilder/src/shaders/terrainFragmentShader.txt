#version 400 core

const int pcfCount = 2;
const float totalTexels = (pcfCount * 2.0 + 1.0) * (pcfCount * 2.0 + 1.0);

in vec2 pass_texCoords;
in vec3 surfaceNormal;
in vec3 toLightVector[4];
in vec3 toCameraVec;
in vec4 shadowCoords;

out vec4 out_colour;

uniform sampler2D textureSampler;
uniform sampler2D causticTexture;
uniform vec3 lightColour[4];
uniform vec3 attenuation[4];
uniform float shineDamper;
uniform float reflectivity;
uniform float causticEffect;
uniform float mapSize;
uniform sampler2D shadowMap;

void main(void)
{
	float texelSize = 1.0 / mapSize;
	float total = 0.0;
	
	for(int x=-pcfCount; x <= pcfCount; x++)
	{
		for(int y=-pcfCount; y <= pcfCount; y++)
		{
			float objectNearestLight = texture(shadowMap, shadowCoords.xy + vec2(x, y) * texelSize).r;
				if(shadowCoords.z > objectNearestLight + 0.002)
				{
					total += 1.0;
				}
		}
	}
	
	total /= totalTexels;
	
	float lightFactor = 1.0 - (total * shadowCoords.w);

	vec3 unitNormal = normalize(surfaceNormal);
	vec3 unitVecToCam = normalize(toCameraVec);
	
	vec3 totalDiffuse = vec3(0.0);
	vec3 totalSpecular = vec3(0.0);
	
	for(int i = 0; i < 4; i++)
	{
		float distance = length(toLightVector[i]);
		float attFactor = attenuation[i].x + (attenuation[i].y * distance) + (attenuation[i].z * distance * distance);
		vec3 unitLightVector = normalize(toLightVector[i]);
		float nDot1 = dot(unitNormal,unitLightVector);
		float brightness = max(nDot1,0.0);
		vec3 lightDir = -unitLightVector;
		vec3 reflectedLightDir = reflect(lightDir,unitNormal);
		float specularFactor = dot(reflectedLightDir, unitVecToCam);
		specularFactor = max(specularFactor,0.0);
		float dampedFactor = pow(specularFactor, shineDamper);
		totalDiffuse = totalDiffuse + (brightness * lightColour[i])/attFactor;
		totalSpecular = totalSpecular + (dampedFactor * reflectivity * lightColour[i])/attFactor;
	}
	totalDiffuse = max(totalDiffuse * lightFactor, 0.2);
	
	out_colour = vec4(totalDiffuse,1.0) * texture(textureSampler,pass_texCoords) + vec4(totalSpecular,1.0);
	vec4 causticTexture = texture(causticTexture, pass_texCoords);
	out_colour = mix(out_colour, causticTexture , causticEffect);
}